package rules
import com.example.demo.model.Log
import com.example.demo.model.AdminAlarm
import com.example.demo.model.AlarmTriggering
import com.example.demo.service.AdminAlarmService
import com.example.demo.service.AlarmTriggeringService

global AdminAlarmService alarmService
global AlarmTriggeringService alarmTriggeringService


rule "Alarm for log status"
	no-loop

    when
		accumulate(
			Log($log: this, !normal, $status: status),
			$logSet: collectSet($status),
			$logList: collectList($status),
			$count: count($log)
		)
		eval($logSet.size() == 1)
		Number(intValue <= $count) from accumulate(
			AdminAlarm(status, param.equalsIgnoreCase($logList.get(0) + ""), $counts: counts) from alarmService.findAll(),
			min($counts)
		)
    then
        alarmTriggeringService.save(new AlarmTriggering($count + " logs with " + $logList.get(0) + " status appeared in the last minute!!"));

end

rule "Alarm for log description"
	no-loop

    when
		accumulate(
			Log($log: this, !normal, $description: description, $userName: userName, $computerName: computerName),
			$logSet: collectSet($description),
			$logList: collectList($description),
			$count: count($log),
			$userNameSet: collectSet($userName),
			$computerNameSet: collectSet($computerName)
		)
		eval($logSet.size() == 1 && ($userNameSet.size() == 1 || $computerNameSet.size() == 1))
		Number(intValue <= $count) from accumulate(
			AdminAlarm(!status, param.equalsIgnoreCase($logList.get(0) + ""), $counts: counts) from alarmService.findAll(),
			min($counts)
		)
    then
        alarmTriggeringService.save(new AlarmTriggering($count + " attempts on '" + $logList.get(0) + "' action appeared in the last minute from the same user/computer!"));

end
